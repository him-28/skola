set term pdfcairo enhanced notransparent color font "Arial,8" size 5cm,3.2cm lw 1 dl 0.5

set style line 1 lc rgb "#e00000" lt 1 pt 5 pi -1
set style line 2 lc rgb "#008000" lt 1 pt 7 pi -1
set style line 3 lc rgb "#f0c010" lt 1 pt 11 pi -1
set style line 4 lc rgb '#0040c0' pt 9
set style textbox opaque noborder

set style line 10 lt 0 lc rgb '#a0a0a0' lw 1.5
set grid ls 10

set xlabel 'Silicon fraction, x'
set ylabel 'Refractive index, n'
set key at graph 0.97,0.95 samplen 1
set pointsize 0.4
set format y '%.1f'

set rmargin 1.5
set tmargin 0.4

set label at graph 0.1,0.1 "Î» = 400 nm" boxed

plot [-0.08:1.08][:3.2]  \
    "anatase-400nm.txt" u 1:(sqrt((sqrt((($2+$4+$6)/3)*(($2+$4+$6)/3)+(($3+$5+$7)/3)*(($3+$5+$7)/3))+(($2+$4+$6)/3))/2)) t 'Anatase calculation' ls 1,\
    "rutile-400nm.txt" u 1:(sqrt((sqrt((($2+$4+$6)/3)*(($2+$4+$6)/3)+(($3+$5+$7)/3)*(($3+$5+$7)/3))+(($2+$4+$6)/3))/2)) t 'Rutile calculation' ls 2,\
	"n.txt" u 1:2 t 'Experiment PECVD' ls 3,\
	"n.txt" u 1:3 t 'Experiment ALD' ls 4
