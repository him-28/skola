set term pdfcairo enhanced notransparent color font "Arial,8" size 6cm,3.4cm lw 1 dl 0.5

set xlabel 'Silicon fraction, x'
set ylabel 'Band gap, E_g (eV)
set format x '%g'
set format y '%.1f'
set key top left reverse invert Left
set pointsize 0.5
set rmargin 1.5
set tmargin 0.0
set bmargin 3.0
set lmargin 10.0

set style line 10 lt 0 lc rgb '#a0a0a0' lw 1.5
set grid ls 10

set style line 1 lc rgb "#e00000" lt 1 pt 5 pi -1
set style line 2 lc rgb "#008000" lt 1 pt 7 pi -1
set style line 3 lc rgb "#f0c010" lt 1 pt 11 pi -1
set style line 4 lc rgb '#0040c0' pt 9
set pointintervalbox 1.6

plot [-0.05:0.88][:4.09]  \
    "gap.txt" u 1:3 t "Rutile calculation"  with linespoints ls 2,\
    "gap.txt" u 1:2 t "Anatase calculation" with linespoints ls 1,\
    "gap-exp.txt" u 1:2 t "Experiment PECVD" ls 3,\
	"gap-exp.txt" u 1:3 t "Experiment ALD" ls 4
