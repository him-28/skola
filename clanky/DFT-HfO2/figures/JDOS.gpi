set term pdfcairo enhanced dashed font "Arial,10" size 8cm,6cm lw 1 dl 0.5

set key at graph 0.4,0.8 reverse Left

set style line 1 lt 1 lw 1 lc rgb 'black' dashtype 1
set style line 2 lt 2 lw 1 lc rgb 'red' dt (20,10)

set multiplot

set rmargin 0.2
set tmargin 0.2
set bmargin 0.2

set size 1,0.43
set origin 0,0.57

botlim = 5.7
toplim = 6.8
exponent = 0.5

set ylabel "sqrt({/Helvetica-Italic J} )"

f(x) = a*x + b
FIT_LIMIT = 1e-10
fit [botlim:toplim] f(x) "slow.outputjoint" u 1:(sqrt($2)) via a,b

Eg = -b/a

set arrow from botlim,0 to botlim,12 nohead
set arrow from toplim,0 to toplim,12 nohead
set format x ''

set label at graph 0.07, 0.88 'slow cooling'
set label at graph 0.1, 0.4 sprintf("{/Helvetica-Italic E}_g = %3.2f", Eg)

set xrange [4.58:7.7]
set yrange [0:15.5]

plot "slow.outputjoint" u 1:(sqrt($2)) w l ls 1 t "data",\
	a*x+b ls 2 t "linear fit"

unset label

FIT_LIMIT = 1e-10
fit [botlim:toplim] f(x) "fast.outputjoint" u 1:(sqrt($2)) via a,b

Eg = -b/a

set origin 0.0,0.14
set xlabel 'Photon energy, {/Helvetica-Italic E} (eV)'
set arrow from botlim,0 to botlim,12 nohead
set arrow from toplim,0 to toplim,12 nohead

set label at graph 0.07, 0.88 'fast cooling'
set label at graph 0.1, 0.4 sprintf("{/Helvetica-Italic E}_g = %3.2f", Eg)
set format x '%.1f'

plot "fast.outputjoint" u 1:(sqrt($2)) w l ls 1 t "data",\
	a*x+b ls 2 t "linear fit"
