set term pdfcairo enhanced color dashed font "Arial,10" size 8cm,6cm lw 1 dl 0.5

load "linestyles.gpi"

set multiplot

set bmargin 0.2
set tmargin 0.2
set rmargin 0.5
set lmargin 6

set xrange [4:10.5]

set ylabel 'Dielectric function, {/Helvetica-Italic ε}_r'
set format x ''
set nokey

set size 1,0.43
set origin 0,0.57

plot [][0:9.5]  \
    "amorphous-fast.epsilon" u 1:(($2+$4+$6)/3) t 'TB-mBJ fast cooling' w l ls 1,\
    "amorphous-slow.epsilon" u 1:(($2+$4+$6)/3) t 'TB-mBJ slow cooling' w l ls 2,\
    "Franta-am-HfO2.dat" u 1:($2*$2-$3*$3) t 'exp [Franta2015]' w l ls 6
set origin 0,0.14
set ylabel 'Dielectric function, {/Helvetica-Italic ε}_i'
set key top left Left reverse
set xlabel 'Photon energy, E [eV]'
set format x '%g'

plot [][0:6.5]  \
    "amorphous-fast.epsilon" u 1:(($3+$5+$7)/3) t 'TB-mBJ fast cooling' w l ls 1,\
    "amorphous-slow.epsilon" u 1:(($3+$5+$7)/3) t 'TB-mBJ slow cooling' w l ls 2,\
    "Nguyen-am-HfO2-eps2.txt" u 1:2 t 'exp [Nguyen2005]' w l ls 5,\
    "Franta-am-HfO2.dat" u 1:(2*$2*$3) t 'exp [Franta2015]' w l ls 6
