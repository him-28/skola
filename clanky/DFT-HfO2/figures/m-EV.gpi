set term pdfcairo enhanced color dashed font "Arial,10" size 8cm,6cm lw 1 dl 0.5

set style line 1 lc rgb '#000000' pt 7 ps 0.3
set style line 2 lc rgb '#5b85ad' pt 7 ps 0.5
set style line 3 lw 1.5 lc rgb '#e00000' dt 1
set style line 4 lw 1.5 lc rgb '#e00000' dt 2

E_0 = -10.2
V_0 = 11.6
B_0 = 14
B_0I = 1

EBM(V) = E_0 + 9*B_0/16 * (((V_0/V)**(2.0/3.0)-1)**3.0 * B_0I + ((V_0/V)**(2.0/3.0) - 1)**2.0 * (6-4*(V_0/V)**(2.0/3.0)))

E_01 = -10.2
V_01 = 14.17
B_01 = 15
B_0I1 = 1

EBM1(V) = E_01 + 9*B_01/16 * (((V_01/V)**(2.0/3.0)-1)**3.0 * B_0I1 + ((V_01/V)**(2.0/3.0) - 1)**2.0 * (6-4*(V_01/V)**(2.0/3.0)))

set multiplot

set bmargin 0.2
set tmargin 0.2
set rmargin 0.2
set lmargin 0

set ylabel 'Total energy per atom (eV)' offset 1.5,0
set format x ''
set format y '%.2f'
set nokey

set size 0.37,0.6
set origin 0.15,0.4
set xrange [8.8:15.5]

set label at graph 0.4,0.9 "GGA"

fit [11.2:12.1] EBM(x) "V-E-gamma.txt" u 1:2 via E_0,B_0,V_0,B_0I
f(x)=( (x<13 && x > 9)? 1 : (1/0) )
ff(x)=( (x<12.1 && x > 11.2)? 1 : (1/0) )

fit [13.55:14.8] EBM1(x) "V-E-gamma.txt" u 1:2 via E_01,B_01,V_01,B_0I1
g(x)=( (x<16 && x > 12.5)? 1 : (1/0) )
gg(x)=( (x<14.8 && x > 13.55)? 1 : (1/0) )

plot [][-10.23:-9.82]\
   "V-E-gamma.txt" u 1:($2*ff($1)) t "" ls 2,\
   "V-E-gamma.txt" u 1:($2*gg($1)) t "" ls 2,\
   "V-E-gamma.txt" u 1:2 t "" ls 1,\
   f(x)*EBM(x) w l ls 3 t "",\
   g(x)*EBM1(x) w l ls 4 t ""

unset label
unset ylabel
set label at graph 0.4,0.9 "LDA"

set origin 0.63,0.4
set key bottom right

E_0 = -11.09
V_0 = 11.01

fit [10.5:11.4] EBM(x) "V-E-gamma_LDA.txt" u 1:2 via E_0,B_0,V_0,B_0I
f(x)=( (x<12.5 && x > 9)? 1 : (1/0) )
ff(x)=( (x<11.4 && x > 10.5)? 1 : (1/0) )

E_01 = -10.99
V_01 = 13.44

fit [13.3:14.6] EBM1(x) "V-E-gamma_LDA.txt" u 1:2 via E_01,B_01,V_01,B_0I1
g(x)=( (x<16 && x > 12.5)? 1 : (1/0) )
gg(x)=( (x<14.6 && x > 13.3)? 1 : (1/0) )

plot [][-11.105:-10.87]\
   "V-E-gamma_LDA.txt" u 1:($2*ff($1)) t "" ls 2,\
   "V-E-gamma_LDA.txt" u 1:($2*gg($1)) t "" ls 2,\
   "V-E-gamma_LDA.txt" u 1:2 t "" ls 1,\
   f(x)*EBM(x) w l ls 3 t "",\
   g(x)*EBM1(x) w l ls 4 t ""

unset label
set size 0.37,0.27
set origin 0.15,0.13
set format y '%.0f'
set format x '%.0f'
set xlabel 'Volume per atom (Å^3)' offset -1,0.5
set ylabel 'γ (°)' offset 0,0
set ytics 10

plot [][87:123]\
    "V-E-gamma.txt" u 1:3 t "" ls 1

unset ylabel
set origin 0.63,0.13

plot [][87:123]\
    "V-E-gamma_LDA.txt" u 1:3 t "" ls 1

unset multiplot
