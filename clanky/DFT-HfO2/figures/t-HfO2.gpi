set term pdfcairo enhanced color dashed font "Arial,10" size 8cm,6cm lw 1 dl 0.5

set ylabel 'Dielectric function, ε_r'
set format x ''
set ytics 2
set nokey

load "linestyles.gpi"

set multiplot

set bmargin 0.2
set tmargin 0.2
set rmargin 0.5
set lmargin 6

set size 1,0.43
set origin 0,0.57

set xrange [4:10.5]

plot [][0:9.5]  \
    "tetragonal.epsilon" u 1:2 t 'TB-mBJ ε_{xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:4 t 'TB-mBJ ε_{zz}' w l ls 3

unset label
set ylabel 'Dielectric function, ε_i'
set format x '%g'
set xlabel 'Photon energy, E [eV]'
set origin 0,0.14
set key top left Left reverse

plot [][:9.5]\
    "tetragonal.epsilon" u 1:3 t 'TB-mBJ ε_{xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:5 t 'TB-mBJ ε_{zz}' w l ls 3

unset multiplot
