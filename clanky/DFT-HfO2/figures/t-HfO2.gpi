set term pdfcairo enhanced color dashed font "Arial,10" size 8cm,6cm lw 1 dl 0.5

set ylabel 'Dielectric function, ε_r'
set format x ''
set format y '%g'
set key bottom left

set style line 1 lt 1 lw 1 lc rgb '#e00000' dashtype 1
set style line 2 lt 2 lw 1 lc rgb '#008000' dashtype 2
set style line 3 lt 3 lw 1 lc rgb '#0040c0' dashtype 3
set style line 4 lt 4 lw 1 lc rgb '#f0c010' dashtype 4
set style line 5 lt 5 lw 1 lc rgb '#a000b0' dashtype 5

set multiplot

set bmargin 0.2
set tmargin 0.2
set rmargin 0.5
set lmargin 6

set size 1,0.43
set origin 0,0.57

set label at graph 0.05,0.9 "a)"

set xrange [4:9.5]

plot [][0:]  \
    "tetragonal.epsilon" u 1:2 t 'calc ε_{1 xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:4 t 'calc ε_{1 zz}' w l ls 3

unset label
set label at graph 0.05,0.9 "b)"
set ylabel 'Dielectric function, ε_i'
set format x '%g'
set xlabel 'Photon energy, E [eV]'
set origin 0,0.14
set key bottom left

plot [][:8.5]\
    "tetragonal.epsilon" u 1:3 t 'calc ε_{2 xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:5 t 'calc ε_{2 zz}' w l ls 3

unset multiplot
