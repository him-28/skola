set term pdfcairo enhanced color dashed font "Arial,10" size 16cm,14cm lw 1 dl 0.5

set format x '%g'
set format y '%g'
set ytics 2
set nokey

load "linestyles.gpi"

set multiplot
set size 0.45,0.24
set xrange [4:10.5]

set bmargin 2.0
set tmargin 0.2
set rmargin 0.2
set lmargin 0.2

set origin 0.04,0.755
set label at graph 0.05,0.88 'monoclinic'

plot [][:9.6]  \
    "monoclinic.epsilon" u 1:2 t 'TB-mBJ {/Helvetica-Italic ε}_{xx}' w l ls 1,\
    "monoclinic.epsilon" u 1:4 t 'TB-mBJ {/Helvetica-Italic ε}_{yy}' w l ls 2,\
    "monoclinic.epsilon" u 1:6 t 'TB-mBJ {/Helvetica-Italic ε}_{zz}' w l ls 3,\
    "monoclinic.epsilon" u 1:10 t 'TB-mBJ {/Helvetica-Italic ε}_{xz}' w l ls 4,\
    "Edwards-m-HfO2-eps1.txt" u 1:2 t 'exp [43]' w l ls 5

unset label
set label at graph 0.05,0.88 'tetragonal'
set ylabel 'Real part of the dielectric function, {/Helvetica-Italic ε}_r' offset 0,-7
set origin 0.04,0.51

plot [][0:9.5]  \
    "tetragonal.epsilon" u 1:2 t 'TB-mBJ {/Helvetica-Italic ε}_{xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:4 t 'TB-mBJ {/Helvetica-Italic ε}_{zz}' w l ls 3

unset label
set label at graph 0.05,0.88 'cubic'
unset ylabel
set origin 0.04,0.265

plot [][0:11]  \
    "cubic.epsilon" u 1:2 t 'TB-mBJ' w l ls 1,\
    "Liu-cHfO2-eps1.txt" u 1:2 t 'exp [37]' w l ls 5

unset label
set label at graph 0.05,0.88 'amorphous'
set origin 0.04, 0.02
set xlabel 'Photon energy, E [eV]'

plot [][0:9.5]  \
    "amorphous-fast.epsilon" u 1:(($2+$4+$6)/3) t 'TB-mBJ fast cooling' w l ls 1,\
    "amorphous-slow.epsilon" u 1:(($2+$4+$6)/3) t 'TB-mBJ slow cooling' w l ls 2,\
    "Franta-am-HfO2.dat" u 1:($2*$2-$3*$3) t 'exp [Franta2015]' w l ls 6

unset label
unset xlabel
set origin 0.55,0.755
set key top left reverse Left

plot [][:9]\
    "monoclinic.epsilon" u 1:3 t 'TB-mBJ {/Helvetica-Italic ε}_{{/Helvetica-Italic xx}}' w l ls 1,\
    "monoclinic.epsilon" u 1:5 t 'TB-mBJ {/Helvetica-Italic ε}_{{/Helvetica-Italic yy}}' w l ls 2,\
    "monoclinic.epsilon" u 1:7 t 'TB-mBJ {/Helvetica-Italic ε}_{{/Helvetica-Italic zz}}' w l ls 3,\
    "monoclinic.epsilon" u 1:11 t 'TB-mBJ {/Helvetica-Italic ε}_{{/Helvetica-Italic xz}}' w l ls 4,\
    "Edwards-m-HfO2-eps2.txt" u 1:2 t 'exp [43]' w l ls 5,\
    "Nguyen-m-HfO2-eps2.txt" u 1:2 t 'exp [40]' w l ls 6
#   "100k_9Rk_hf_PBE.epsilon_br01" u 1:3 t 'YS-PBE0 {/Helvetica-Italic ε}_{{/Helvetica-Italic xx}}' w l ls 7,\
#   "monoclinic-BSE.epsilon" u ($1+0.8):($2/3) t 'TB-mBJ BSE' w l ls 8,\

set ylabel 'Imaginary part of the dielectric function, {/Helvetica-Italic ε}_i' offset 0,-7
set origin 0.55,0.51

plot [][:9.5]\
    "tetragonal.epsilon" u 1:3 t 'TB-mBJ {/Helvetica-Italic ε}_{/Helvetica-Italic xx}' w l ls 1,\
    "tetragonal.epsilon" u 1:5 t 'TB-mBJ {/Helvetica-Italic ε}_{/Helvetica-Italic zz}' w l ls 3,\
#   "tetragonal-BSE.epsilon" u 1:3 t 'TB-mBJ BSE {/Helvetica-Italic ε}_{/Helvetica-Italic xx}' w l ls 2,\
#   "tetragonal-BSE.epsilon" u 1:5 t 'TB-mBJ BSE {/Helvetica-Italic ε}_{/Helvetica-Italic zz}' w l ls 4

unset ylabel
set origin 0.55,0.265

plot [][:]\
    "cubic.epsilon" u 1:3 t 'TB-mBJ RPA' w l ls 1,\
    "Liu-cHfO2-eps2.txt" u 1:2 t 'exp [37]' w l ls 5
#   "cubic-BSE.epsilon" u 1:3 t 'TB-mBJ BSE' w l ls 2,\
#   "cubic-semi-BSE.epsilon" u 1:3 t 'TB-mBJ BSE' w l ls 3,\

set origin 0.55,0.02
set xlabel 'Photon energy, E [eV]'

plot [][0:6.5]  \
    "amorphous-fast.epsilon" u 1:(($3+$5+$7)/3) t 'TB-mBJ fast cool.' w l ls 1,\
    "amorphous-slow.epsilon" u 1:(($3+$5+$7)/3) t 'TB-mBJ slow cool.' w l ls 2,\
    "Nguyen-am-HfO2-eps2.txt" u 1:2 t 'exp [40]' w l ls 5,\
    "Franta-am-HfO2.dat" u 1:(2*$2*$3) t 'exp [44]' w l ls 6

unset multiplot
