set term pdfcairo enhanced notransparent truecolor dashed font "Arial,10" size 8cm,7.3cm lw 1.2
set style textbox opaque noborder

set multiplot layout 2,1 margins 0.1,0.995,0.13,0.995 spacing 0.045,0.015

unset colorbox
set palette defined ( 0 "dark-blue", 1 "red", 2 "goldenrod", 3 "green")

set style data line
do for [i=1:10] {
    set linetype i palette
}

set style line 10 lt 0 lc rgb '#a0a0a0' lw 1.5
set grid lc 10

set key reverse Left samplen 1.5 spacing 1.1

set xrange [2.8:11.5]
set yrange [0:9.5]

set label "PECVD experiment" at graph 0.3,0.93 boxed
set format x ''

plot \
    'nk/0.0-PECVD-old.nk' u (1240/$1):(2*$2*$3):(0.0) t '{/:Italics x} = 1.00',\
    'nk/0.18-PECVD-new.nk' u (1240/$1):(2*$2*$3):(0.18) t '{/:Italics x} = 0.82',\
    'nk/0.28-PECVD-new.nk' u (1240/$1):(2*$2*$3):(0.28) t '{/:Italics x} = 0.72',\
    'nk/0.38-PECVD-old.nk' u (1240/$1):(2*$2*$3):(0.4) t '{/:Italics x} = 0.60',\
    'nk/0.55-PECVD-old.nk' u (1240/$1):(2*$2*$3):(0.61) t '{/:Italics x} = 0.39',\
    'nk/0.74-PECVD-old.nk' u (1240/$1):(2*$2*$3):(0.74) t '{/:Italics x} = 0.26',\
    'nk/0.85-PECVD-old.nk' u (1240/$1):(2*$2*$3):(0.85) t '{/:Italics x} = 0.15',\
    'nk/1.0-PECVD-old.nk' u (1240/$1):(2*$2*$3):(1.0) t '{/:Italics x} = 0.00'

unset label
set label "Amorphous calculations" at graph 0.3,0.93 boxed
unset format x
set ylabel 'Imaginary part of the dielectric function, {/:Italic Îµ}_i' offset 0,6
set xlabel 'Photon energy, {/:Italic E} (eV)'
set key width -3

plot \
    'data/am-0.0.epsilon' u 1:(($3+$5+$7)/3):(0.0) t '{/:Italics x} = 1.0',\
    'data/anatase-0.0.epsilon' u 1:(($3+$5+$7)/3):(0.0) w l dashtype '.-_' t '{/:Italics x} = 1.0 anatas' ,\
    'data/am-0.1875.epsilon' u 1:(($3+$5+$7)/3):(0.188) t '{/:Italics x} = 0.813',\
    'data/am-0.34375.epsilon' u 1:(($3+$5+$7)/3):(0.344) t '{/:Italics x} = 0.656',\
    'data/am-0.5.epsilon' u 1:(($3+$5+$7)/3):(0.5) t '{/:Italics x} = 0.5',\
    'data/am-0.65625.epsilon' u 1:(($3+$5+$7)/3):(0.656) t '{/:Italics x} = 0.344',\
    'data/am-0.8125.epsilon' u 1:(($3+$5+$7)/3):(0.813) t '{/:Italics x} = 0.188',\
    'data/am-1.0.epsilon' u 1:(($3+$5+$7)/3):(1.0) t '{/:Italics x} = 0.0'

