set term pdfcairo enhanced color dashed font "Arial,10" size 11cm,6cm lw 1.0 dl 0.5

set multiplot
set size 0.42,1.0
set lmargin 0.0
set rmargin 0.1
set tmargin 4.8

set ylabel 'Intensity (a.u.)' offset 2,0
set xlabel 'Binding energy, E [eV]'
set key above Left reverse

set label at graph 0.05,0.90 "Exp."

set origin 0.09,0.0

set style line 1 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#004cc8'
set style line 2 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#2d3997'
set style line 3 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#462f7b'
set style line 4 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#6b1f53'
set style line 5 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#970d23'
set style line 6 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#b00606'
set style line 7 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#9b1818'
set style line 8 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#7d2727'
set style line 9 lt 1 pt 65 ps 0.3 lw 1.4 lc rgb '#393838'

plot [-534.9:-528.2]  \
    "data/XPS-exp-Stephane.txt" u (-$1):($2/2.966) w l ls 1 t 'x=0.0',\
    "data/XPS-exp-Stephane.txt" u (-$1):($3/2.352) w l ls 2 t 'x=0.1',\
    "data/XPS-exp-Stephane.txt" u (-$1):($4/2.305) w l ls 3 t 'x=0.16',\
    "data/XPS-exp-Stephane.txt" u (-$1):($5/2.000) w l ls 4 t 'x=0.23',\
    "data/XPS-exp-Stephane.txt" u (-$1):($6/1.973) w l ls 5 t 'x=0.33',\
    "data/XPS-exp-Stephane.txt" u (-$1):($7/1.433) w l ls 6 t 'x=0.53',\
    "data/XPS-exp-Stephane.txt" u (-$1):($9/1.600) w l ls 8 t 'x=0.77',\
    "data/XPS-exp-Stephane.txt" u (-$1):($10/1.574) w l ls 9 t 'x=1.0'

unset label
unset ylabel
set label at graph 0.05,0.90 "Calc."
set origin 0.58,0.0

plot [-540.1:-534.5][-0.1:] \
   "< awk '{print $1,$NF}' data/1.0-O-XPS-spectra-imp.txt" u 1:2 w l ls 1 t 'x=0.00',\
   "< awk '{print $1,$NF}' data/0.8125-O-XPS-spectra-imp.txt" u 1:2 w l ls 3 t 'x=0.19',\
   "< awk '{print $1,$NF}' data/0.65625-O-XPS-spectra-imp.txt" u 1:2 w l ls 4 t 'x=0.34',\
   "< awk '{print $1,$NF}' data/0.5-O-XPS-spectra-imp.txt" u 1:2 w l ls 5 t 'x=0.50',\
   "< awk '{print $1,$NF}' data/0.34375-O-XPS-spectra-imp.txt" u 1:2 w l ls 6 t 'x=0.66',\
   "< awk '{print $1,$NF}' data/0.1875-O-XPS-spectra-imp.txt" u 1:2 w l ls 8 t 'x=0.81',\
   "< awk '{print $1,$NF}' data/0.0-O-XPS-spectra-imp.txt" u 1:2 w l ls 9 t 'x=1.00'
