set term pdfcairo enhanced notransparent color font "Arial,8" size 9cm,5cm lw 1.5 dl 0.5

set style data line
set style textbox opaque noborder

set style line 1 lt 1 lc rgb '#e00000'
set style line 2 lt 2 lc rgb '#008000'
set style line 3 lt 3 lc rgb '#0040c0'
set style line 4 lt 4 lc rgb '#f0c010'
set style line 5 lt 5 lc rgb '#a000b0'
set style line 6 lt 1 lc rgb '#f08000'
set style line 7 lt 1 lc rgb '#00c0c0'
set style line 8 lt 1 lc rgb 'magenta'
set style line 9 lt 1 lc rgb '#000000'

set xlabel 'Photon energy, E (eV)'
set ylabel 'Dielectric function, ε_i'
set format x '%g'
set format y '%g'
set key center top

set title "HfO_2 calculated dielectric function compared with experiment"

set arrow from 3,9.9 to 10.7,9.9 heads size screen 0.003,90
set label "spectrophotometric data" at 4,10.4 boxed

set arrow from 3,8.5 to 8.7,8.5 heads size screen 0.003,90
set label "ellipsometric data" at 4,9.0 boxed

set arrow from 40,9.9 to 30,9.9 heads size screen 0.003,90
set label "X-ray tables" at 35,10.4 boxed

plot [3:40][-1:11] \
    "HfO2_cubic.epsilon" u 1:3 t 'cubic' w l ls 7,\
    "HfO2_monoclinic.epsilon" u 1:3 t 'monoclinic ε_{xx}' w l ls 2,\
    "HfO2_monoclinic.epsilon" u 1:5 t 'monoclinic ε_{yy}' w l ls 3,\
    "HfO2_monoclinic.epsilon" u 1:7 t 'monoclinic ε_{zz}' w l ls 4,\
    "HfO2_monoclinic.epsilon" u 1:11 t 'monoclinic ε_{xz}' w l ls 6,\
    "HfO2_am.epsilon" u 1:(($3+$5+$7)/3) t 'amorphous ε' w l ls 9,\
    "HfO2.nk" u (1240/$1):(2*$2*$3) t 'dispersion model old' w l ls 5,\
    "HfO2-new.nk" u (1240/$1):(2*$2*$3) t 'dispersion model new' w l ls 1

