set terminal pdf enhanced color font "Helvetica,12" linewidth 1 size 10cm,7cm
set locale "cs_CZ.UTF-8"
set encoding utf8
set output "BM-EOS.pdf"

set title "Birch-Murnaghan EOS"
set ylabel "Total energy"
set format y "%.3f"

a = 1
b = 1
c = -975000
para(V) = a*V*V + b*V + c

E_0 = -975906
V_0 = 7650.0
B_0 = 70.0
B_0I = 6.0

EBM(V) = E_0 + 9*B_0/16 * (((V_0/V)**(2.0/3.0)-1)**3.0 * B_0I + ((V_0/V)**(2.0/3.0) - 1)**2.0 * (6-4*(V_0/V)**(2.0/3.0)))

E_0M = -975908.49
V_0M = 7800
K_0 = 1
K_0I = 2

EM(V) = E_0M + K_0 * V_0M * (1/(K_0I*(K_0I-1))*(V/V_0M)**(1-K_0I) + 1/K_0I*V/V_0M - 1/(K_0I-1))


#fit para(x) "EV.txt" via a,b,c
fit EBM(x) "EV.txt" via E_0,B_0,V_0,B_0I
fit EM(x) "EV.txt" via E_0M,K_0,K_0I,V_0M

plot "EV.txt" u 1:2, EM(x) w l t "Murnaghan EOS", EBM(x) w l t "Birch-Murnaghan EOS"
